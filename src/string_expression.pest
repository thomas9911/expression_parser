file = _{SOI ~ file_inner ~ EOI}
file_inner = _{ (line ~ (seperator ~ line)* ~ seperator?) }
line = _{ unassign | assign | importing | expr }
seperator = _{";"+}

// // Function
function = !{ "{" ~ function_variables ~ "=>" ~ file_inner ~ "}" }
function_variables = !{ (var ~ ("," ~ var)*)? }

// // Assignment
assignment = _{ SOI ~ (unassign | assign) ~ EOI }

unassign = {"unset" ~ var }
assign = {var ~ "=" ~ expr}

// // Import
importing = {"import" ~ import_map ~ "from" ~ import_from}
    import_map = {"{" ~ map_assignment ~ ("," ~ map_assignment)* ~ "}"}
    map_assignment = { (var ~ ": " ~ var) | var }
    import_from = { string }

// // Expression

calculation = _{ SOI ~ expr ~ EOI }

num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
    int = { ("+" | "-")? ~ ASCII_DIGIT+ }

var = @{ (var_char)+ }
    var_char = { 'a'..'z' | 'A'..'Z' | '0'..'9' | "_" | colon }
    colon = { ":" ~ var_char }

string  = ${ "\"" ~ inner ~ "\"" }
    inner   = _{ char ~ (escape ~ inner)? }
    // escape  = { "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t") }
    escape  = _{ escaped | "\\" ~ (unicode) }
    escaped = {"\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")}
    unicode = _{ rust_unicode | json_unicode }
    rust_unicode = _{ "u" ~ "{" ~  unicode_code ~ "}" }
    json_unicode = _{ "u" ~ unicode_code }
        unicode_code = {ASCII_HEX_DIGIT{2,6}}
        char = {(!("\"" | "\\") ~ ANY)*}

// string  = @{ "\"" ~ letters ~ "\"" }
//     letters = @{ (!("\"" | "\\") ~ char)* }
//     char = _{ PUNCTUATION | LETTER | NUMBER | WHITESPACE }

operation = _{ string_operation | binary_operation | number_operation}

string_operation = _{ concat_op }
    concat_op = { "++" }

binary_operation = _{ equal | not_equal | lesser | greater | and | or }
    equal     = { "==" }
    not_equal = { "!=" }
    lesser    = { "<" }
    greater   = { ">" }
    and       = { "&" | "&&" |"and" }
    or        = { "|" | "||" | "or" }

number_operation = _{ power | add | subtract | multiply | divide }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    power    = { "^" | "**" }

dot_function = ${ var_or_func ~ dot_operator ~ "(" ~ spaces ~ any_arguments ~ spaces  ~ ")"}
    var_or_func = _{ func | var | function }
    dot_operator = { "." }

expr = !{ term ~ (operation ~ term)* }

func = ${ func_name ~ "(" ~  spaces ~ any_arguments ~ spaces ~ ")" }
    any_arguments = _{ one_or_more_arguments? }
    one_or_more_arguments = _{ spaces ~ expr ~ (spaces ~ COMMA ~ spaces ~ expr)* }

    func_name = { ('a'..'z')+ }

list = { "[" ~ any_arguments ~ "]"}

map = !{ ("{" ~ map_item ~ (COMMA ~ map_item)* ~ "}") | ("{" ~ "}") }
    map_item = _{ string ~ ":" ~ expr }

term = _{ dot_function | num | "(" ~ expr ~ ")" | string | func | var | list | map | function }

WHITESPACE = _{ " " | "\t" | NEWLINE }
spaces = _{ WHITESPACE* }
COMMA = _{","}

COMMENT =  _{ one_line_comment | multi_line_comment }
one_line_comment =  _{ ("//" | "#") ~ (!"\n" ~ ANY)* }
multi_line_comment = _{"/*" ~ (!"*/" ~ ANY)* ~ "*/"}